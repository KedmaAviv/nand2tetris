// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/1/DMux8Way.hdl
/**
 * 8-way demultiplexor:
 * [a, b, c, d, e, f, g, h] = [in, 0,  0,  0,  0,  0,  0,  0] if sel = 000
 *                            [0, in,  0,  0,  0,  0,  0,  0] if sel = 001
 *                            [0,  0, in,  0,  0,  0,  0,  0] if sel = 010
 *                            [0,  0,  0, in,  0,  0,  0,  0] if sel = 011
 *                            [0,  0,  0,  0, in,  0,  0,  0] if sel = 100
 *                            [0,  0,  0,  0,  0, in,  0,  0] if sel = 101
 *                            [0,  0,  0,  0,  0,  0, in,  0] if sel = 110
 *                            [0,  0,  0,  0,  0,  0,  0, in] if sel = 111
 */
CHIP DMux8Way {
    IN in, sel[3];
    OUT a, b, c, d, e, f, g, h;

    PARTS:
    // hierarchical demux: split by sel[2], then split each half by sel[1] and sel[0]
    DMux (in=in, sel=sel[2], a=top, b=bottom);
    // top half
    DMux (in=top, sel=sel[1], a=top0, b=top1);
    DMux (in=top0, sel=sel[0], a=a, b=b);
    DMux (in=top1, sel=sel[0], a=c, b=d);
    // bottom half
    DMux (in=bottom, sel=sel[1], a=bot0, b=bot1);
    DMux (in=bot0, sel=sel[0], a=e, b=f);
    DMux (in=bot1, sel=sel[0], a=g, b=h);
}
